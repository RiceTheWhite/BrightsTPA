plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'xyz.jpenilla.run-paper' version "2.3.1"
    id 'java'
}

group = gradle.projectGroup
version = gradle.projectVersion

tasks {
    shadowJar {
        archiveBaseName.set("${project.name}")
        archiveClassifier.set("")
    }

    runServer {
        minecraftVersion("1.21.8")
        jvmArgs("-Xms2G", "-Xmx2G")
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven{url("https://repo.papermc.io/repository/maven-public/")}
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
}

def targetJavaVersion = gradle.javaVersion as int

java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

    if (JavaVersion.current() < JavaVersion.toVersion(targetJavaVersion)) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion as String)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [
        version: project.version,
        group: project.group,
        name: project.name,
        apiVersion: gradle.apiVersion
    ]

    inputs.properties props
    filesMatching('plugin.yml') {
        expand props
    }
    filteringCharset 'UTF-8'
}
