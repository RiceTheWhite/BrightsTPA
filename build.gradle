plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'xyz.jpenilla.run-paper' version "2.3.1"
    id 'java'
}

tasks {
    shadowJar {
        archiveBaseName.set("${project.name}")
        archiveClassifier.set("")
    }

    tasks.jar {
        enabled = false
    }

    runServer {
        minecraftVersion("1.21.8")
    }
}

static def getLatestTag() {
    try {
        'git fetch --tags'.execute().waitFor()

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()

        if (commit.isEmpty()) return "unknown"

        def tagProcess = 'git describe --tags --abbrev=0'.execute()
        tagProcess.waitFor()
        def rawTag = tagProcess.text.trim()

        def hasTag = rawTag.contains('v')

        def branch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()

        if (hasTag) {
            def tag = rawTag - 'v'
            return branch == "release" ? tag : "$tag+$commit"
        } else {
            return "1.0.0+$commit"
        }
    } catch (Exception ignored) {
        return "unknown"
    }
}

version = getLatestTag()

allprojects {
    repositories {
        mavenCentral()
        maven{url("https://repo.papermc.io/repository/maven-public/")}
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
}

java {
    sourceCompatibility = JavaVersion.toVersion(javaVersion)
    targetCompatibility = JavaVersion.toVersion(javaVersion)

    if (JavaVersion.current() < JavaVersion.toVersion(javaVersion)) {
        toolchain.languageVersion = JavaLanguageVersion.of(javaVersion as String)
    }
}

def javaVersion = javaVersion as int

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (javaVersion as int >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = javaVersion as int
    }
}

processResources {
    def props = [
        version: project.version,
        group: project.group,
        name: project.name,
        apiVersion: apiVersion
    ]

    inputs.properties props
    filesMatching('plugin.yml') {
        expand props
    }
    filteringCharset 'UTF-8'
}
